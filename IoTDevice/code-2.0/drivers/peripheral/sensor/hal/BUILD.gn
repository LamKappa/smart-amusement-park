#
#Copyright (c) 2021 Huawei Device Co., Ltd.
#Licensed under the Apache License, Version 2.0 (the "License");
#you may not use this file except in compliance with the License.
#You may obtain a copy of the License at
#
#    http://www.apache.org/licenses/LICENSE-2.0
#
#Unless required by applicable law or agreed to in writing, software
#distributed under the License is distributed on an "AS IS" BASIS,
#WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
#See the License for the specific language governing permissions and
#limitations under the License.
#
if (defined(ohos_lite)) {
  import("//build/lite/config/component/lite_component.gni")
} else {
  import("//build/ohos.gni")
}

SENSOR_ROOT_DIR = "//drivers/peripheral/sensor"

if (defined(ohos_lite)) {
  FWK_ROOT_DIR = "//drivers/framework"
  shared_library("hdi_sensor") {
    sources = [
      "$SENSOR_ROOT_DIR/hal/src/sensor_channel.c",
      "$SENSOR_ROOT_DIR/hal/src/sensor_core.c",
      "$SENSOR_ROOT_DIR/hal/src/sensor_manager.c",
    ]

    include_dirs = [
      "$SENSOR_ROOT_DIR/hal/include",
      "$SENSOR_ROOT_DIR/interfaces/include",
      "$FWK_ROOT_DIR/include",
      "$FWK_ROOT_DIR/include/utils",
      "$FWK_ROOT_DIR/include/osal",
      "$FWK_ROOT_DIR/include/core",
      "$FWK_ROOT_DIR/ability/sbuf/include",
      "//drivers/adapter/uhdf/posix/include",
      "//third_party/bounds_checking_function/include",
    ]

    deps = [
      "//base/hiviewdfx/hilog_lite/frameworks/featured:hilog_shared",
      "//drivers/adapter/uhdf/manager:hdf_core",
      "//drivers/adapter/uhdf/posix:hdf_posix_osal",
      "//third_party/bounds_checking_function:libsec_shared",
    ]
    public_deps = [ "//third_party/bounds_checking_function:libsec_shared" ]

    cflags = [
      "-Wall",
      "-Wextra",
      "-Werror",
      "-fsigned-char",
      "-fno-common",
      "-fno-strict-aliasing",
    ]
  }

  lite_component("hdi") {
    features = [ ":hdi_sensor" ]
  }
} else {
  config("libhdi_sensor_pub_config") {
    visibility = [ ":*" ]

    include_dirs = [ "$SENSOR_ROOT_DIR/interfaces/include" ]
  }

  ohos_shared_library("hdi_sensor") {
    public_configs = [ ":libhdi_sensor_pub_config" ]
    sources = [
      "$SENSOR_ROOT_DIR/hal/src/sensor_channel.c",
      "$SENSOR_ROOT_DIR/hal/src/sensor_core.c",
      "$SENSOR_ROOT_DIR/hal/src/sensor_manager.c",
    ]

    include_dirs = [
      "$SENSOR_ROOT_DIR/hal/include",
      "$SENSOR_ROOT_DIR/interfaces/include",
      "//third_party/bounds_checking_function/include",
    ]

    deps = [
      "//drivers/adapter/uhdf2/osal:libhdf_utils",
      "//utils/native/base:utils",
    ]
    defines = [ "__OHOS__USER__" ]
    cflags = [
      "-Wall",
      "-Wextra",
      "-Werror",
      "-fsigned-char",
      "-fno-common",
      "-fno-strict-aliasing",
    ]
    subsystem_name = "hdf"
    if (is_standard_system) {
      external_deps = [ "hiviewdfx_hilog_native:libhilog" ]
    } else {
      external_deps = [ "hilog:libhilog" ]
    }
  }
}
