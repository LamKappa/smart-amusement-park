# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//foundation/ace/ace_engine/ace_config.gni")

source_set("ace_resource") {
  deps = [ ":binary_resource" ]

  # add resource file here
  sources = [
    "internal_resource.cpp",
    "shared_image_manager.cpp",
  ]

  configs += [ "$ace_root:ace_config" ]

  if (current_os == "mingw") {
    cflags = [ "-std=c++17" ]
  }
}

source_set("binary_resource") {
  if (current_os == "mac") {
    ace_resource_dir = "$ace_root/frameworks/base/resource/binary/mac/"
  } else {
    deps = [ ":objcopy_resource" ]
    ace_resource_dir =
        get_label_info(":binary_resource($current_toolchain)", "target_out_dir")
  }
  sources = [
    "$ace_resource_dir/close.o",
    "$ace_resource_dir/correct.o",
    "$ace_resource_dir/down_arrow.o",
    "$ace_resource_dir/entry.o",
    "$ace_resource_dir/errorcode.o",
    "$ace_resource_dir/exit_full_screen.o",
    "$ace_resource_dir/full_screen.o",
    "$ace_resource_dir/hide_password.o",
    "$ace_resource_dir/ic_back.o",
    "$ace_resource_dir/ic_more.o",
    "$ace_resource_dir/ic_more_dark.o",
    "$ace_resource_dir/ic_more_transparent.o",
    "$ace_resource_dir/indexer_arrow.o",
    "$ace_resource_dir/indexletter_bar.o",
    "$ace_resource_dir/left_arrow.o",
    "$ace_resource_dir/pause.o",
    "$ace_resource_dir/piece_delete.o",
    "$ace_resource_dir/play.o",
    "$ace_resource_dir/rate_star_big_off.o",
    "$ace_resource_dir/rate_star_big_on.o",
    "$ace_resource_dir/rate_star_small_off.o",
    "$ace_resource_dir/rate_star_small_on.o",
    "$ace_resource_dir/right_arrow.o",
    "$ace_resource_dir/search.o",
    "$ace_resource_dir/select_arrow_disabled.o",
    "$ace_resource_dir/select_arrow_normal.o",
    "$ace_resource_dir/semi_modal_bar_down.o",
    "$ace_resource_dir/semi_modal_bar_middle.o",
    "$ace_resource_dir/share.o",
    "$ace_resource_dir/show_password.o",
    "$ace_resource_dir/stepper_back_arrow.o",
    "$ace_resource_dir/stepper_next_arrow.o",
    "$ace_resource_dir/translate_c2e.o",
    "$ace_resource_dir/tv_rate_star_big_off.o",
    "$ace_resource_dir/tv_rate_star_big_on.o",
    "$ace_resource_dir/tv_rate_star_small_off.o",
    "$ace_resource_dir/tv_rate_star_small_on.o",
    "$ace_resource_dir/up_arrow.o",
    "$ace_resource_dir/watch_down_arrow.o",
    "$ace_resource_dir/watch_up_arrow.o",
    "$ace_resource_dir/wrong.o",
  ]
}

action_foreach("objcopy_resource") {
  # add resource file here
  sources = [
    "binary/close.svg",
    "binary/correct.svg",
    "binary/down_arrow.svg",
    "binary/exit_full_screen.svg",
    "binary/full_screen.svg",
    "binary/hide_password.svg",
    "binary/i18n/entry.json",
    "binary/i18n/errorcode.json",
    "binary/ic_back.svg",
    "binary/ic_more.svg",
    "binary/ic_more_dark.svg",
    "binary/ic_more_transparent.svg",
    "binary/indexer_arrow.png",
    "binary/indexletter_bar.json",
    "binary/left_arrow.svg",
    "binary/pause.svg",
    "binary/piece_delete.svg",
    "binary/play.svg",
    "binary/rate_star_big_off.svg",
    "binary/rate_star_big_on.svg",
    "binary/rate_star_small_off.svg",
    "binary/rate_star_small_on.svg",
    "binary/right_arrow.svg",
    "binary/search.svg",
    "binary/select_arrow_disabled.png",
    "binary/select_arrow_normal.png",
    "binary/semi_modal_bar_down.svg",
    "binary/semi_modal_bar_middle.svg",
    "binary/share.svg",
    "binary/show_password.svg",
    "binary/stepper_back_arrow.svg",
    "binary/stepper_next_arrow.svg",
    "binary/translate_c2e.svg",
    "binary/tv_rate_star_big_off.svg",
    "binary/tv_rate_star_big_on.svg",
    "binary/tv_rate_star_small_off.svg",
    "binary/tv_rate_star_small_on.svg",
    "binary/up_arrow.svg",
    "binary/watch_down_arrow.svg",
    "binary/watch_up_arrow.svg",
    "binary/wrong.svg",
  ]

  ace_resource_dir =
      get_label_info(":binary_resource($current_toolchain)", "target_out_dir")

  outputs = [ "$ace_resource_dir/{{source_name_part}}.o" ]

  objcopy_tool = objcopy_aarch64
  script = "$flutter_root/engine/flutter/sky/tools/objcopy.py"

  if (use_mingw_win) {
    objcopy_tool = objcopy_mingw
    script = "$flutter_root/engine/flutter/sky/tools/objcopy_pc_mac.py"
  } else if (use_mac) {
    objcopy_tool = objcopy_clang
    script = "$flutter_root/engine/flutter/sky/tools/objcopy_pc_mac.py"
  } else if (target_cpu == "x86_64") {
    objcopy_tool = objcopy_x86_64
  }

  args = [
    "--objcopy",
    rebase_path("$objcopy_tool"),
    "--input",
    "{{source}}",
    "--output",
    rebase_path("$ace_resource_dir/") + "{{source_name_part}}.o",
    "--arch",
    current_cpu,
  ]
}
