# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/ace/ace_engine/ace_config.gni")

config("ace_base_i18n_config") {
  defines = [ "ACEL10N" ]

  include_dirs = [ "//utils/native/base/include" ]
}

template("ace_base_i18n") {
  forward_variables_from(invoker, "*")
  source_set(target_name) {
    defines += invoker.defines
    deps = [
      "$ace_root/frameworks/base/resource:ace_resource",
      "$flutter_root:ace_libicu_$platform",
    ]

    configs += [
      ":ace_base_i18n_config",
      "$ace_root:ace_config",
      "$flutter_root:icu_config_$platform",
    ]

    # add base source file here
    sources = [ "localization.cpp" ]

    if (use_mingw_win) {
      cflags = [
        "-std=c++17",

        # Notice: this flag actually apply to icu source code.
        "-Wno-ignored-attributes",
      ]
    }
  }
}

ace_base_i18n("ace_base_i18n_ohos") {
  defines = ace_ohos_defines
  platform = "ohos"
}

ace_base_i18n("ace_base_i18n_windows") {
  defines = ace_windows_defines
  platform = "windows"
}

ace_base_i18n("ace_base_i18n_mac") {
  defines = ace_mac_defines
  platform = "mac"
}
